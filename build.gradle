task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
    distributionUrl = "https://github.com/10QueensRoad/gradle/releases/download/REL_${gradleVersion}-java8/gradle-${gradleVersion}-bin.zip"
}

apply plugin: 'maven'

group = 'org.fossbrain.green'
version = '0.1.0-SNAPSHOT'

apply plugin: 'scala'

// Separate the itests from the regular integration tests
sourceSets {
    itest {
        scala.srcDir file('src/itest/scala')
        resources.srcDir file('src/itest/resources')
    }
}

task itest(type: Test) {
    testClassesDir = sourceSets.itest.output.classesDir
    classpath = sourceSets.itest.runtimeClasspath
}

check.dependsOn itest

repositories {
    mavenCentral()
}

dependencies {
    // this is an old version required since the new version
    // does not play together nicley with sbt/zinc I assume.
    // maybe we should try to upgrade this from time to time if
    // new version support to take the latest version again
    // maybe we should try to upgrade this from time to time if
    compile 'org.scala-lang:scala-library:2.10.4'
    compile 'org.scala-lang:scala-compiler:2.10.4'

    // this one is part of the incremental compiler and needs
    // to be removed by time with a working ant again
    compile 'com.typesafe.zinc:zinc:0.3.5'

    // testing libraries
    testCompile 'junit:junit:4.11'
    testCompile 'org.scalatest:scalatest_2.10:2.1.7'
    // testRuntime 'org.scala-lang.modules:scala-xml_2.11:1.0.1'

    itestCompile sourceSets.main.output
    itestCompile configurations.testCompile
    itestCompile sourceSets.test.output
    itestRuntime configurations.testRuntime
}

// uses the zinc version of the scala compiler instead of the ant one
// since ant isn't compatible to java 8; is is incremental, but for CI
// builds (since it is slower for non-incremental build) it's not
// recommended by now. This needs to be reevaluated from time to time
tasks.withType(ScalaCompile) {
    scalaCompileOptions.useAnt = false
}

apply plugin: 'application'

mainClassName = "Main"

applicationDistribution.from('.') {
    include('README.md')
    rename('README.md', 'README')

    include('NOTICE.md')
    rename('NOTICE.md', 'NOTICE')

    include('CHANGELOG.md')
    rename('CHANGELOG.md', 'CHANGELOG')

    include('LICENSE')
}
